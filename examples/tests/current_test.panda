module example-test

require 'current_test_required'

import org.panda_lang.panda.PandaConstants

import java.lang.System
import org.panda_lang.panda.utilities.commons.StringUtils

class Console {

    static method print(Object... values) {
        System.out.println(StringUtils.toString(values));
    }

}

class Foo extends Test {

    static method anotherEcho(String message1) {
        Console.print(message1)
    }

}

class Test {

    hidden mut Test testField
    hidden Double double = 1.5D
    hidden Int index = 1

    constructor(String message2) {
        Console.print(message2)
        this.testField = this
        testField = this
        this.echo(String.valueOf(double))
        echo(this)
    }

    constructor() {
        // empty constructor
    }

    method echo(Object message3) {
        Console.print(message3)
    }

    method testField() {
        this.echo(this.getTestField())
        return
    }

    method Test getTestField() {
        return this.testField
    }

}

main {
    late String init
    nil Object req = null

    Test test = new Test('"Constructor"')
    test.echo("Method")
    Console.print(test.getTestField().testField)

    mut nil Foo varFoo = null
    varFoo = new Foo()

    mut String s = "Hello Panda v1"
    s = "Hello Panda v2"

    Int i = 0x000001
    Number math = 10.1 + (3 + 4.2 + i) * 2 /*+ "test" */
    Boolean flag = i == 1

    Console.print("Hello Panda", flag, varFoo, s, test, i)
    Console.print(math)

    if (!flag) {
        Console.print("Nope")
    }
    else {
        Console.print("Yay")
    }

    List list = new ArrayList()
    list.add("val1")
    list.add("val2")

    StringBuilder builder = new StringBuilder()
    builder.append(list)

    loop (i) {
        builder.append(" Loop x1 ")

        foreach (String var : list) {
            builder.append(var)
        }

        continue
        builder.append(" Loop x3 ")
    }

    while (flag != false) {
        Console.print(builder.toString())
        break
    }

    TestArray testArray = new TestArray(7);
    testArray.modify(test)

    Console.print("Hello " + "Array " + testArray.array.toString())
    Console.print("OR v AND: " + (false || false) + ", " + false || true, true && false, true && true)
    Console.print("Compare: " + 1 > 2, 1 > 2, 1 < 2, 2 < 1)

    Int creaseValue = 10
    Console.print("Increase: " + creaseValue++, creaseValue++, ++creaseValue, ++creaseValue)
    Console.print("Decrease: " + creaseValue--, creaseValue--, --creaseValue, --creaseValue)

    creaseValue++
    Console.print(creaseValue)

    // 3849 3839 | 479 113 = 3660 expr |
    //Console.print("Random",  (false || false) + ", " + false || true, true && false, true && true, (false || false) + ", " + false || true, true && false, true && true)
    Console.print("Random",  (false || false) + ", " + false || true, true && false, true && true, (false || false) + ", " + false || true, true && false, true && true)

    Required required = new Required()
    required.hello()

    try {
        throw new RuntimeException("Try")
    } catch (Exception e) {
        Console.print(e.getMessage())
    }

    Console.print(PandaConstants.VERSION)
}

class TestArray {

    hidden String[] array

    constructor(Int size) {
        this.array = new String[size];
    }

    local method modify(Test test) {
        this.getArray()[test.index] = "Hello Array"
        array[6] = String.valueOf(this)

        Console.print("Value at array[test.index]", this.getArray()[test.index])
    }

    method String[] getArray() {
        return array
    }

}